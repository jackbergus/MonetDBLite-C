# The contents of this file are subject to the MonetDB Public License
# Version 1.1 (the "License"); you may not use this file except in
# compliance with the License. You may obtain a copy of the License at
# http://monetdb.cwi.nl/Legal/MonetDBLicense-1.1.html
#
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
# License for the specific language governing rights and limitations
# under the License.
#
# The Original Code is the MonetDB Database System.
#
# The Initial Developer of the Original Code is CWI.
# Portions created by CWI are Copyright (C) 1997-July 2008 CWI.
# Copyright August 2008-2010 MonetDB B.V.
# All Rights Reserved.

dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.60)
AC_INIT([MonetDB geometry module], [0.17.0], [monet@cwi.nl], [MonetDB-geom])
#                                   ^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AC_CONFIG_AUX_DIR(conf)
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AH_TOP([#include "sysdefs.h"])
AH_BOTTOM([#include "stddef.h"])

dnl ----------------------
AM_INIT_AUTOMAKE
AC_CONFIG_SRCDIR([src/lib/libgeom.mx])
AM_CONFIG_HEADER(geom_config.h:conf/config.h.in)
CONFIG_H=geom_config.h
AC_SUBST(CONFIG_H)

# GEOM_BUILD and GEOM_SOURCE are *only* used for monetdb-geom-config and RunMserver
# GEOM_SOURCE is the absolute path name of the source directory
# (srcdir can be relative)

# The Q versions of various variables are used in places where \'s
# need to be escapes with an extra \.  Configure does not support \'s
# in path names, so there is no need to do anything special here
# except set the variables.  The command to set them in case we do
# need to escape the \'s is Qvar=`echo "$var" | sed 's/\\\\/\\\\\\\\/g'`
GEOM_BUILD=[`pwd`]
AC_SUBST(GEOM_BUILD)

GEOM_SOURCE=[`(cd $srcdir && pwd)`]
AC_SUBST(GEOM_SOURCE)

AM_MONETDB_XQ_VARS_1()


dnl Check for Monet
req_monetdb_ver='1.36.0'
#                ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AM_MONETDB_COMMON($req_monetdb_ver)
if test "x$have_monetdb" != xyes; then
	AC_MSG_ERROR([MonetDB geometry requires at least version $req_monetdb_ver of MonetDB, but only version $MONETDB_VERSION was found.])
fi
AM_MONETDB_DEFAULTS()
AM_MONETDB_COMPILER()
AM_MONETDB_TOOLS()
AM_MONETDB_OPTIONS()
AM_MONETDB_LIBS()
AM_MONETDB_UTILS()
req_monetdb4_ver='4.36.0'
#                 ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AM_MONETDB4($req_monetdb4_ver)
req_monetdb5_ver='5.18.0'
#                 ^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AM_MONETDB5($req_monetdb5_ver)

if test "x$have_monetdb4" != xyes -a "x$have_monetdb5" != xyes; then
	if test "x$MONETDB4_VERSION" = "x" -a "x$MONETDB5_VERSION" = "x"; then
		AC_MSG_ERROR([Neither MonetDB4 nor MonetDB5 was not found. MonetDB/Geom requires at least version $req_monetdb4_ver of MonetDB4 or version $req_monetdb5_ver of MonetDB5.])
	else
		if test "x$MONETDB4_VERSION" = "x"; then
			MONETDB4_VERSION='none'
		fi
		if test "x$MONETDB5_VERSION" = "x"; then
			MONETDB5_VERSION='none'
		fi
		AC_MSG_ERROR([MonetDB/Geom requires at least version $req_monetdb4_ver of MonetDB4 (found $MONETDB4_VERSION) or version $req_monetdb5_ver of MonetDB5 (found $MONETDB5_VERSION).])
	fi
fi

dnl geos
GEOS_VERSION_REQUIRED_MIN=2.2.0
dnl GEOS_VERSION_REQUIRED_MAX=3.0.0
have_geos=auto
GEOS_CONFIG=''
GEOS_INCS=''
GEOS_LIBS=''
AC_ARG_WITH(geos,
	AC_HELP_STRING([--with-geos=DIR],
		[geos library is installed in DIR]),
	have_geos="$withval")
if test "x$have_geos" != xno; then
	case "$have_geos" in
	yes|auto)
		XPATH="$PATH"
		;;
	*)
		XPATH="$have_geos/bin"
		;;
	esac
	AC_PATH_PROG(GEOS_CONFIG,geos-config,,$XPATH)
	if test "x$GEOS_CONFIG" = x; then
		AC_MSG_ERROR([geos-config not found in $XPATH])
		have_geos=no
	fi
fi
if test "x$have_geos" != xno; then
dnl	AC_MSG_CHECKING([for Geos >= $GEOS_VERSION_REQUIRED_MIN, but < $GEOS_VERSION_REQUIRED_MAX])
	AC_MSG_CHECKING([for Geos >= $GEOS_VERSION_REQUIRED_MIN])
	GEOS_VERSION="`$GEOS_CONFIG --version`"
	AC_MSG_RESULT(found $GEOS_VERSION)
	wrong=''
	if test MONETDB_VERSION_TO_NUMBER(echo $GEOS_VERSION) -lt MONETDB_VERSION_TO_NUMBER(echo $GEOS_VERSION_REQUIRED_MIN); then
		wrong="too old"
	fi
dnl	if test MONETDB_VERSION_TO_NUMBER(echo $GEOS_VERSION) -ge MONETDB_VERSION_TO_NUMBER(echo $GEOS_VERSION_REQUIRED_MAX); then
dnl		wrong="not yet supported"
dnl	fi
	if test "x$wrong" != x; then
		AC_MSG_ERROR([Geos version $GEOS_VERSION is $wrong])
		have_geos=no
	fi
fi
if test "x$have_geos" != xno; then
	case "$GCC-$CC-`$GEOS_CONFIG --prefix`-`$GEOS_CONFIG --includes`" in
	-*icc*-/usr-/usr/include)
		dnl  icc complains about Goes' header file, at least if Geos
		dnl  is found in the default place and we set GEOS_INCS & GEOS_LIBS.
		dnl  Since setting GEOS_INCS & GEOS_LIBS is not strictly required
		dnl  in this case, we omit setting them in this case.
		;;
	*)
		dnl GEOS_INCS="-I`$GEOS_CONFIG --includes`"
		dnl GEOS_LIBS="`$GEOS_CONFIG --libs`"
		GEOS_INCS="`$GEOS_CONFIG --cflags`"
		GEOS_LIBS="`$GEOS_CONFIG --ldflags`"
		;;
	esac
	save_CPPFLAGS="$CPPFLAGS"
	CPPFLAGS="$CPPFLAGS $GEOS_INCS"
	AC_CHECK_HEADER(geos_c.h, AC_DEFINE(HAVE_GEOS_C_H, 1, [Define to 1 if you have the <geos_c.h> header file.]), [AC_MSG_ERROR([geos_c.h not found]); have_geos=no])
	CPPFLAGS="$save_CPPFLAGS"
fi
if test "x$have_geos" != xno; then
	save_LIBS="$LIBS"
    	LIBS="$LIBS $GEOS_LIBS"
    	AC_CHECK_LIB(geos_c, GEOSEnvelope, :, [AC_MSG_ERROR([-lgeos_c not found]); have_geos=no])
    	LIBS="$save_LIBS"
fi
if test "x$have_geos" != xno; then
	GEOS_LIBS="$GEOS_LIBS -lgeos_c"
	AC_DEFINE(HAVE_SQLGETPRIVATEPROFILESTRING, 1, [Define if you have the SQLGetPrivateProfileString function])
fi
AC_SUBST(GEOS_INCS)
AC_SUBST(GEOS_LIBS)


dnl provide different versions of the paths derived above
AM_MONETDB_XQ_VARS_2()
AC_DEFINE_UNQUOTED(GEOM_PREFIX,"$QXprefix",[architecture-independent files])
AC_DEFINE_UNQUOTED(GEOM_EXEC_PREFIX,"$QXexec_prefix",[architecture-dependent files])
QGEOM_BUILD="$GEOM_BUILD"
XGEOM_BUILD="`$translatepath "$GEOM_BUILD"`"
QXGEOM_BUILD="`echo "$XGEOM_BUILD" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QGEOM_BUILD)
AC_SUBST(XGEOM_BUILD)
AC_SUBST(QXGEOM_BUILD)
QGEOM_SOURCE="$GEOM_SOURCE"
XGEOM_SOURCE="`$translatepath "$GEOM_SOURCE"`"
QXGEOM_SOURCE="`echo "$XGEOM_SOURCE" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QGEOM_SOURCE)
AC_SUBST(XGEOM_SOURCE)
AC_SUBST(QXGEOM_SOURCE)
AC_DEFINE_UNQUOTED(GEOM_LOCALSTATEDIR,"$QXlocalstatedir",[modifiable single-machine data])
AC_DEFINE_UNQUOTED(GEOM_LIBDIR,"$QXlibdir",[object code libraries])
AC_DEFINE_UNQUOTED(GEOM_SYSCONFDIR,"$QXsysconfdir",[read-only single-machine data])


dnl  CFLAGS for our code are stricter than what autoconf can cope with.
CFLAGS="$CFLAGS \$(X_CFLAGS)"

case "$host_os" in
*mingw*)
AM_CONDITIONAL(NOT_WIN32, false)
AM_CONDITIONAL(NATIVE_WIN32, true)
;;
*)
AM_CONDITIONAL(NOT_WIN32, true)
AM_CONDITIONAL(NATIVE_WIN32, false)
;;
esac
