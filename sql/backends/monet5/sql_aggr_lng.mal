# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0.  If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# Copyright 1997 - July 2008 CWI, August 2008 - 2016 MonetDB B.V.

function aggr.covar(e1:bat[:oid,:lng], e2:bat[:oid,:lng]):lng;
	e0:bat[:oid,:lng] := batcalc.*(e1,e2);
	s0:lng := aggr.sum(e0);
	s1:lng := aggr.sum(e1);
	s2:lng := aggr.sum(e2);
	v2:lng := calc.*(s1,s2);
	c := aggr.count(e1);
	n:lng := calc.lng(c);
	v3:lng := calc./(v2,n);
	v1:lng := calc.-(s0,v3);
	v:lng := calc./(v1,n);
	return v;
end aggr.covar;

function aggr.corr(e1:bat[:oid,:lng], e2:bat[:oid,:lng]):lng;
	cv:lng := aggr.covar(e1,e2);
	sd1 := aggr.stdev(e1);
	sd2 := aggr.stdev(e2);
	sd := calc.*(sd1,sd2);
	r := calc./(cv,sd);
	res := calc.lng(r);
	return res;
end aggr.corr;

function aggr.covar(e1:bat[:oid,:lng], e2:bat[:oid,:lng], g:bat[:oid,:oid], e:bat[:oid,:any_2]):bat[:oid,:lng];
	e0:bat[:oid,:lng] := batcalc.*(e1,e2);
	s0:bat[:oid,:lng] := aggr.sum(e0,g,e);
	s1:bat[:oid,:lng] := aggr.sum(e1,g,e);
	s2:bat[:oid,:lng] := aggr.sum(e2,g,e);
	v2:bat[:oid,:lng] := batcalc.*(s1,s2);
	c := aggr.count(e1,g,e);
	n:bat[:oid,:lng] := batcalc.lng(c);
	v3:bat[:oid,:lng] := batcalc./(v2,n);
	v1:bat[:oid,:lng] := batcalc.-(s0,v3);
	v:bat[:oid,:lng] := batcalc./(v1,n);
	return v;
end aggr.covar;

function aggr.corr(e1:bat[:oid,:lng], e2:bat[:oid,:lng], g:bat[:oid,:oid],e:bat[:oid,:any_2]):bat[:oid,:lng];
	cv:bat[:oid,:lng] := aggr.covar(e1,e2,g,e);
	sd1 := aggr.stdevp(e1,g,e);
	sd2 := aggr.stdevp(e2,g,e);
	sd := batcalc.*(sd1,sd2);
	sdn := batcalc.==(sd,0:lng);
	sds := batcalc.ifthenelse(sdn,1:lng,sd);
	r := batcalc./(cv,sds);
	res := batcalc.lng(r);
	return res;
end aggr.corr;
