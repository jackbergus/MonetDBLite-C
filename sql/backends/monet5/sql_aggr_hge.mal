# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0.  If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# Copyright 1997 - July 2008 CWI, August 2008 - 2016 MonetDB B.V.

function aggr.covar(e1:bat[:oid,:hge], e2:bat[:oid,:hge]):hge;
	e0:bat[:oid,:hge] := batcalc.*(e1,e2);
	s0:hge := aggr.sum(e0);
	s1:hge := aggr.sum(e1);
	s2:hge := aggr.sum(e2);
	v2:hge := calc.*(s1,s2);
	c := aggr.count(e1);
	n:hge := calc.hge(c);
	v3:hge := calc./(v2,n);
	v1:hge := calc.-(s0,v3);
	v:hge := calc./(v1,n);
	return v;
end aggr.covar;

function aggr.corr(e1:bat[:oid,:hge], e2:bat[:oid,:hge]):hge;
	cv:hge := aggr.covar(e1,e2);
	sd1 := aggr.stdev(e1);
	sd2 := aggr.stdev(e2);
	sd := calc.*(sd1,sd2);
	r := calc./(cv,sd);
	res := calc.hge(r);
	return res;
end aggr.corr;

function aggr.subcovar(e1:bat[:oid,:hge], e2:bat[:oid,:hge], g:bat[:oid,:oid], e:bat[:oid,:any_2],f:bit):bat[:oid,:hge];
	e0:bat[:oid,:hge] := batcalc.*(e1,e2);
	s0:bat[:oid,:hge] := aggr.subsum(e0,g,e,f,true);
	s1:bat[:oid,:hge] := aggr.subsum(e1,g,e,f,true);
	s2:bat[:oid,:hge] := aggr.subsum(e2,g,e,f,true);
	v2:bat[:oid,:hge] := batcalc.*(s1,s2);
	c := aggr.subcount(e1,g,e,f);
	n:bat[:oid,:hge] := batcalc.hge(c);
	v3:bat[:oid,:hge] := batcalc./(v2,n);
	v1:bat[:oid,:hge] := batcalc.-(s0,v3);
	v:bat[:oid,:hge] := batcalc./(v1,n);
	return v;
end aggr.subcovar;

function aggr.subcorr(e1:bat[:oid,:hge], e2:bat[:oid,:hge], g:bat[:oid,:oid],e:bat[:oid,:any_2],f:bit):bat[:oid,:hge];
	cv:bat[:oid,:hge] := aggr.subcovar(e1,e2,g,e,f);
	sd1 := aggr.substdevp(e1,g,e,f,true);
	sd2 := aggr.substdevp(e2,g,e,f,true);
	sd := batcalc.*(sd1,sd2);
	sdn := batcalc.==(sd,0:hge);
	sds := batcalc.ifthenelse(sdn,1:dbl,sd);
	r := batcalc./(cv,sds);
	res := batcalc.hge(r);
	return res;
end aggr.subcorr;
