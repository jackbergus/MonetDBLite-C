#                                               -*- Autoconf -*-
#                                               vim: ft=config :

# The contents of this file are subject to the MonetDB Public License
# Version 1.1 (the "License"); you may not use this file except in
# compliance with the License. You may obtain a copy of the License at
# http://monetdb.cwi.nl/Legal/MonetDBLicense-1.1.html
#
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
# License for the specific language governing rights and limitations
# under the License.
#
# The Original Code is the MonetDB Database System.
#
# The Initial Developer of the Original Code is CWI.
# Portions created by CWI are Copyright (C) 1997-July 2008 CWI.
# Copyright August 2008-2011 MonetDB B.V.
# All Rights Reserved.

dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.60)
AC_INIT([MonetDB Server], [5.23.0], [info@monetdb.org], [MonetDB5-server])
#                          ^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AC_CONFIG_AUX_DIR(conf)
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AH_BOTTOM([#include "stddef.h"])

dnl ----------------------
dnl We have filenames larger than 99 chars, so use ustar to allow up to
dnl 256 characters.  tar-pax is too young, and might result in problems
dnl on anchient systems.
AM_INIT_AUTOMAKE([tar-ustar])
AC_CONFIG_SRCDIR([src/mal/mal.mx])
AM_CONFIG_HEADER(mal_config.h:conf/config.h.in)
CONFIG_H=mal_config.h
AC_SUBST(CONFIG_H)

HOST=[$target]
AC_DEFINE_UNQUOTED(HOST, "$HOST", [Host identifier])

if test x"$target_os" = xcygwin; then
	AC_DEFINE(WIN32, 1, [Define on MS Windows (also under Cygwin)])
fi

PKG_PROG_PKG_CONFIG()

# MONETDB5_BUILD and MONETDB5_SOURCE are *only* used for monetdb5-config
# MONETDB5_SOURCE is the absolute path name of the source directory
# (srcdir can be relative)

# The Q versions of various variables are used in places where \'s
# need to be escapes with an extra \.  Configure does not support \'s
# in path names, so there is no need to do anything special here
# except set the variables.  The command to set them in case we do
# need to escape the \'s is Qvar=`echo "$var" | sed 's/\\\\/\\\\\\\\/g'`
MONETDB5_BUILD=[`pwd`]
AC_SUBST(MONETDB5_BUILD)

MONETDB5_SOURCE=[`(cd $srcdir && pwd)`]
AC_SUBST(MONETDB5_SOURCE)

AM_MONETDB_XQ_VARS_1()


dnl Check for Monet
# Check for Monet 
req_monetdb_ver='1.40.0'
#                ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.

PKG_CHECK_MODULES([monetdbstream], [monetdb-stream >= $req_monetdb_ver])
PKG_CHECK_MODULES([monetdbcommon], [monetdb-mutils >= $req_monetdb_ver,
                                    monetdb-gdk >= $req_monetdb_ver])

req_clients_ver='1.40.0'
#                ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.

PKG_CHECK_MODULES([monetdbmapi], [monetdb-mapi >= $req_clients_ver])

AM_MONETDB_DEFAULTS()
AM_MONETDB_COMPILER()
AM_MONETDB_OPTIONS()
AM_MONETDB_UTILS()
AM_MONETDB_TYPES()
AM_MONETDB_PATH_FILE()
AM_MONETDB_TOOLS()
dnl AM_MONETDB_LIBS()
AM_MONETDB_LIB_PTHREAD()
AM_MONETDB_LIB_M()
AM_MONETDB_LIB_MALLOC()
AM_MONETDB_LIB_DL()
AM_MONETDB_LIB_SOCKET()
AM_MONETDB_LIB_HWCOUNTERS()

dnl src/modules/mal/mal_mapi.mx does UNIX domain socket handling
AM_MONETDB_MSG_CONTROL()
AM_ICONV()

req_pcre_ver='4.5'
PKG_CHECK_MODULES([pcre], [libpcre >= $req_pcre_ver])
AC_PATH_PROG(PCRETEST,pcretest)
if test "x$PCRETEST" = x; then
	AC_MSG_ERROR([could not find pcretest])
else
	AC_MSG_CHECKING(whether pcre comes with UTF-8 support)
	pcre_utf8="`$PCRETEST -C 2>/dev/null | grep 'UTF-8 support' | sed -e 's|^ *||' -e 's| *$||'`"
	if test "x$pcre_utf8" != "xUTF-8 support"; then
		AC_MSG_ERROR([PCRE library compiled without UTF-8 support])
	fi
	AC_MSG_RESULT([yes])
fi

PKG_CHECK_MODULES([openssl], [openssl >= 0.9.8f])

PKG_CHECK_EXISTS([libxml-2.0], [have_libxml2="yes"], [have_libxml2="no"])
if test x"$have_libxml2" = x"yes" ; then
	PKG_CHECK_MODULES([libxml2], [libxml-2.0])
	AC_DEFINE(HAVE_LIBXML2, 1, [Define if you have the libxml2 library])
fi
AM_CONDITIONAL(HAVE_LIBXML2, test x"$have_libxml2" != xno)

req_raptor_ver='1.4.16'
PKG_CHECK_EXISTS([raptor >= $req_raptor_ver], [have_raptor="yes"], [have_raptor="no"])
if test x"$have_raptor" = x"yes" ; then
	PKG_CHECK_MODULES([raptor], [raptor >= $req_raptor_ver])
fi
AM_CONDITIONAL(HAVE_RAPTOR, test x"$have_raptor" != xno)

PKG_CHECK_EXISTS([libcurl], [have_curl="yes"], [have_curl="no"])
if test x"$have_curl" = x"yes" ; then
	PKG_CHECK_MODULES([curl], [libcurl])
fi
AM_CONDITIONAL(HAVE_CURL, test x"$have_curl" != xno)

PKG_CHECK_EXISTS([zlib], [have_zlib="yes"], [have_zlib="no"])
if test x"$have_zlib" = x"yes" ; then
	PKG_CHECK_MODULES([zlib], [zlib])
fi
AM_CONDITIONAL(HAVE_LIBZ, test x"$have_zlib" != xno)

AC_PROG_LIBTOOL

dnl Checks for header files.
AC_HEADER_TIME()
AC_CHECK_HEADERS([crypt.h fcntl.h ieeefp.h malloc.h sys/resource.h sys/socket.h sys/time.h sys/un.h termios.h time.h xmmintrin.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TRY_COMPILE([$ac_includes_default
#include <malloc.h>], [struct mallinfo m;], AC_DEFINE(HAVE_STRUCT_MALLINFO, 1, [Define if you have struct mallinfo])
AC_TRY_COMPILE([$ac_includes_default
#include <malloc.h>], [struct mallinfo m = {0};
/* compilers that don't like <0 comparisons on unsigned data *
 * will fail here (but then not in src/gdk/gdk_utils.mx)     */
int x = (m.usmblks < 0);], AC_DEFINE(HAVE_SIGNED_MALLINFO, 1, [Define if your mallinfo struct has signed elements])
))
AC_CACHE_CHECK(for _sys_siglist, mn_cv_have__sys_siglist, [
AC_TRY_LINK([$ac_includes_default], [printf("%s\n", _sys_siglist[0]);], mn_cv_have__sys_siglist=yes, mn_cv_have__sys_siglist=no)])
if test $mn_cv_have__sys_siglist = yes; then
	AC_DEFINE(HAVE__SYS_SIGLIST, 1, [Define if you have _sys_siglist])
fi

AC_COMPILE_IFELSE(AC_LANG_SOURCE([int foo(int * restrict p) { return *p; }]),
AC_DEFINE(HAVE_RESTRICT, 1, [Define if the compiler supports the restrict keyword]),
AC_COMPILE_IFELSE(AC_LANG_SOURCE([int foo(int * __restrict__ p) { return *p; }]),
AC_DEFINE(HAVE___RESTRICT__, 1, [Define if the compiler supports the __restrict__ keyword])))

dnl Checks for library functions.
AC_SEARCH_LIBS(crypt, crypt, AC_DEFINE(HAVE_CRYPT, 1, [crypt]))
AC_CHECK_FUNCS([asctime_r ctime_r fpclass fpclassify ftime isinf localtime_r putenv sbrk setenv setsid sigaction strncasecmp strcasestr strtoll sysconf strptime strftime])

asctime_r3=yes
AC_MSG_CHECKING(asctime_r3)
AC_TRY_LINK([$ac_includes_default
#include <time.h>], 
[char buf[26]; struct tm t; asctime_r(&t,buf,26);],
[AC_DEFINE(HAVE_ASCTIME_R3, 1, [Define if you have asctime_r(struct tm*,char *buf,size_t s)])], [asctime_r3=no])
AC_MSG_RESULT($asctime_r3)

ctime_r3=yes
AC_MSG_CHECKING(ctime_r3)
AC_TRY_LINK([$ac_includes_default
#include <time.h>], 
[char buf[26]; time_t t; ctime_r(&t,buf,26);],
[AC_DEFINE(HAVE_CTIME_R3, 1, [Define if you have ctime_r(time_t*,char *buf,size_t s)])], [ctime_r3=no])
AC_MSG_RESULT($ctime_r3)


have_sphinxclient="auto"
AC_ARG_WITH(sphinxclient,
    AS_HELP_STRING([--with-sphinxclient=DIR],[sphinxclient library is installed in DIR]),
    [have_sphinxclient="$withval"], [have_sphinxclient="auto"])
if test "x$have_sphinxclient" != xno; then
	case "$have_sphinxclient" in
	auto|yes)
	    ;;
	*)
	    SPHINXCLIENT_CFLAGS="-I$have_sphinxclient/include"
	    SPHINXCLIENT_LIBS="-L$have_sphinxclient/lib"
	    ;;
	esac

	save_CPPFLAGS="$CPPFLAGS"
	save_LDFLAGS="$LDFLAGS"
	CPPFLAGS="$CPPFLAGS $SPHINXCLIENT_CFLAGS"
	LDFLAGS="$LDFLAGS $SPHINXCLIENT_LIBS"
	AC_CHECK_HEADER(sphinxclient.h,
		AC_CHECK_LIB(sphinxclient, sphinx_create,
			AC_DEFINE(HAVE_SPHINXCLIENT, 1, [Define if you have the sphinxclient library])
			have_sphinxclient=yes
			SPHINXCLIENT_LIBS="$SPHINXCLIENT_LIBS -lsphinxclient",
			[ if test "x$have_sphinxclient" != xauto; then AC_MSG_ERROR([-lsphinxclient library not found]); fi; have_sphinxclient=no ]),
		[ if test "x$have_sphinxclient" != xauto; then AC_MSG_ERROR([sphinxclient.h header not found]); fi; have_sphinxclient=no ])
	LDFLAGS="$save_LDFLAGS"
	CPPFLAGS="$save_CPPFLAGS"
fi
AC_SUBST(SPHINXCLIENT_CFLAGS, $SPHINXCLIENT_CFLAGS)
AC_SUBST(SPHINXCLIENT_LIBS, $SPHINXCLIENT_LIBS)
AM_CONDITIONAL(HAVE_SPHINXCLIENT, test x"$have_sphinxclient" != xno)

dnl RIPEMD160 is patent free, academic and European, but unfortunately
dnl can't use it by default, as that would exclude JDBC usage (Java
dnl doesn't natively support RIPEMD160).
MONETDB5_PASSWDHASH="SHA512"
AC_ARG_WITH(password-backend,
	AS_HELP_STRING([--with-password-backend=HASHALG],
		[password hash algorithm, one of MD5, SHA1, RIPEMD160, SHA224, SHA256, SHA384, SHA512, defaults to $MONETDB5_PASSWDHASH]),
	password_backend="$withval")
case "$password_backend" in
	yes|no|auto|"")
	;;
	MD5|SHA1|RIPEMD160|SHA224|SHA256|SHA384|SHA512)
		MONETDB5_PASSWDHASH="$password_backend"
	;;
	*)
		AC_MSG_ERROR(['$password_backend' invalid, choose one of MD5, SHA1, RIPEMD160, SHA224, SHA256, SHA384, SHA512])
	;;
esac
AC_DEFINE_UNQUOTED([MONETDB5_PASSWDHASH], "$MONETDB5_PASSWDHASH", [The used password hash algorithm])

dnl The console is a direct client hooked onto the kernel with full
dnl administrative privileges, bypassing any security checks.  It is
dnl handy only during development.
dnl We simply can't disable it for now because Testing gets hopelessly
dnl upset about it, but we can offer to the user to disable it for
dnl increased security.
use_console=yes
AC_ARG_ENABLE(console,
	AS_HELP_STRING([--enable-console],
		[enables direct console on the server (involves security risks)]),
	use_console="$withval")
if test x$use_console = xyes ; then
	AC_DEFINE([HAVE_CONSOLE], 1, [If the console should be used])
fi

dnl provide different versions of the paths derived above
AM_MONETDB_XQ_VARS_2()
AC_DEFINE_UNQUOTED(MONETDB5_PREFIX,"$QXprefix",[architecture-independent files])
AC_DEFINE_UNQUOTED(MONETDB5_EXEC_PREFIX,"$QXexec_prefix",[architecture-dependent files])
QMONETDB5_BUILD="$MONETDB5_BUILD"
XMONETDB5_BUILD="`$translatepath "$MONETDB5_BUILD"`"
QXMONETDB5_BUILD="`echo "$XMONETDB5_BUILD" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QMONETDB5_BUILD)
AC_SUBST(XMONETDB5_BUILD)
AC_SUBST(QXMONETDB5_BUILD)
QMONETDB5_SOURCE="$MONETDB5_SOURCE"
XMONETDB5_SOURCE="`$translatepath "$MONETDB5_SOURCE"`"
QXMONETDB5_SOURCE="`echo "$XMONETDB5_SOURCE" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QMONETDB5_SOURCE)
AC_SUBST(XMONETDB5_SOURCE)
AC_SUBST(QXMONETDB5_SOURCE)
AC_DEFINE_UNQUOTED(MONETDB5_LOCALSTATEDIR,"$QXlocalstatedir",[modifiable single-machine data])
AC_DEFINE_UNQUOTED(MONETDB5_LIBDIR,"$QXlibdir",[object code libraries])
AC_DEFINE_UNQUOTED(MONETDB5_SYSCONFDIR,"$QXsysconfdir",[read-only single-machine data])
AC_DEFINE_UNQUOTED(MONETDB5_CONFFILE,"$QXsysconfdir${QDIRSEP}monetdb5.conf",[MonetDB5 config file])


dnl allow us to keep track of how we are going to build this thing
compilercall="$CC $CFLAGS $X_CFLAGS"
AC_SUBST(compilercall)
linkercall="$LD $LDFLAGS"
AC_SUBST(linkercall)
builtby="${USER}@`hostname`"
AC_SUBST(builtby)
pcreversion="(not linked to any PCRE library)"
if test "x$have_pcre" = xyes; then
	pcreversion="compiled with `$PCRE_CONFIG --version 2>/dev/null`"
fi
AC_SUBST(pcreversion)
opensslversion="(not linked to any openssl library)"
if test "x$have_openssl" != xno; then
	opensslversion="compiled with `openssl version 2>/dev/null`"
fi
AC_SUBST(opensslversion)
libxml2version="(not linked to any libxml2 library)"
if test "x$have_libxml2" = xyes; then
	libxml2version="compiled with `$XML2_CONFIG --version 2>/dev/null`"
fi
AC_SUBST(libxml2version)

dnl  CFLAGS for our code are stricter than what autoconf can cope with.
CFLAGS="$CFLAGS \$(X_CFLAGS)"
