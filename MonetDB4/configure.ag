dnl -*- mode: autoconf -*-

# The contents of this file are subject to the MonetDB Public License
# Version 1.1 (the "License"); you may not use this file except in
# compliance with the License. You may obtain a copy of the License at
# http://monetdb.cwi.nl/Legal/MonetDBLicense-1.1.html
#
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
# License for the specific language governing rights and limitations
# under the License.
#
# The Original Code is the MonetDB Database System.
#
# The Initial Developer of the Original Code is CWI.
# Portions created by CWI are Copyright (C) 1997-July 2008 CWI.
# Copyright August 2008-2010 MonetDB B.V.
# All Rights Reserved.

dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.60)
AC_INIT([MonetDB Server], [4.38.5], [info@monetdb.org], [MonetDB4-server])
#                          ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AC_CONFIG_AUX_DIR(conf)
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AH_TOP([#include "sysdefs.h"])
AH_BOTTOM([#include "stddef.h"])

dnl ----------------------
AM_INIT_AUTOMAKE([tar-ustar])
AC_CONFIG_SRCDIR([src/monet/monet.mx])
AM_CONFIG_HEADER(monetdb4_config.h:conf/config.h.in)
CONFIG_H=monetdb4_config.h
AC_SUBST(CONFIG_H)

HOST=[$target]
AC_DEFINE_UNQUOTED(HOST, "$HOST", [Host identifier])

if test x"$target_os" = xcygwin; then
	AC_DEFINE(WIN32, 1, [Define on MS Windows (also under Cygwin)])
fi

# MONETDB4_BUILD and MONETDB4_SOURCE are *only* used for monetdb4-config
# MONETDB4_SOURCE is the absolute path name of the source directory
# (srcdir can be relative)

# The Q versions of various variables are used in places where \'s
# need to be escapes with an extra \.  Configure does not support \'s
# in path names, so there is no need to do anything special here
# except set the variables.  The command to set them in case we do
# need to escape the \'s is Qvar=`echo "$var" | sed 's/\\\\/\\\\\\\\/g'`
MONETDB4_BUILD=[`pwd`]
AC_SUBST(MONETDB4_BUILD)

MONETDB4_SOURCE=[`(cd $srcdir && pwd)`]
AC_SUBST(MONETDB4_SOURCE)

AM_MONETDB_XQ_VARS_1()


# Check for Monet 
req_monetdb_ver='1.38.0'
#                ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AM_MONETDB_COMMON($req_monetdb_ver)
if test "x$have_monetdb" != xyes; then
	if test "x$MONETDB_VERSION" = "x"; then
		AC_MSG_ERROR([MonetDB was not found. MonetDB4 requires at least version $req_monetdb_ver of MonetDB.])
	else
		AC_MSG_ERROR([MonetDB4 requires at least version $req_monetdb_ver of MonetDB, but only version $MONETDB_VERSION was found.])
	fi
fi
req_clients_ver='1.38.0'
#                ^^^^^^
# Maintained via vertoo. Please don't modify by hand!
# Contact MonetDB-developers@lists.sourceforge.net for details and/or assistance.
AM_MONETDB_CLIENTS($req_clients_ver)
AM_MONETDB_DEFAULTS()
AM_MONETDB_COMPILER()
AM_MONETDB_TOOLS()
AM_MONETDB_OPTIONS()
AM_MONETDB_LIBS()

AC_PROG_LIBTOOL

dnl check for NetCDF io library (default /usr and /usr/local)
have_netcdf=$dft_netcdf
NETCDF_CFLAGS=""
NETCDF_LIBS=""
AC_ARG_WITH(netcdf,
	AS_HELP_STRING([--with-netcdf=DIR],
		[netcdf library is installed in DIR]),
	have_netcdf="$withval")
case "$have_netcdf" in
yes|no|auto)
	;;
*)
	NETCDF_CFLAGS="-I$withval/include"
	NETCDF_LIBS="-L$withval/lib"
	;;
esac
if test "x$have_netcdf" != xno; then
	save_CPPFLAGS="$CPPFLAGS"
	CPPFLAGS="$CPPFLAGS $NETCDF_CFLAGS"
	save_LIBS="$LIBS"
	LIBS="$LIBS $NETCDF_LIBS -lnetcdf"
	AC_LINK_IFELSE(AC_LANG_PROGRAM([#include <netcdf/netcdf.h>], [(void) nc_open("",0,(int*)0);]),
		NETCDF_LIBS="$NETCDF_LIBS -lnetcdf",
		[ if test "x$have_netcdf" != xauto; then AC_MSG_ERROR([netcdf library not found]); fi; have_netcdf=no ])
	LIBS="$save_LIBS"
	CPPFLAGS="$save_CPPFLAGS"
fi
if test "x$have_netcdf" != xno; then
	AC_DEFINE(HAVE_LIBNETCDF, 1, [Define if you have the netcdf library])
else
	NETCDF_CFLAGS=""
	NETCDF_LIBS=""
fi
AC_SUBST(NETCDF_CFLAGS)
AC_SUBST(NETCDF_LIBS)
AM_CONDITIONAL(HAVE_NETCDF, test "x$have_netcdf" != xno)
NETCDF=$have_netcdf
AC_SUBST(NETCDF)

dnl check for the performance counters library 
have_pcl=auto
PCL_CFLAGS=""
PCL_LIBS=""
AC_ARG_WITH(pcl,
	AS_HELP_STRING([--with-pcl=DIR],
		[pcl library is installed in DIR]),
	have_pcl="$withval")
AC_MSG_CHECKING(for libpcl)
case "$have_pcl" in
yes|no|auto)
	;;
*)
	PCL_CFLAGS="-I$withval/include"
	PCL_LIBS="-L$withval/lib"
        AC_MSG_CHECKING(in $withval) 
	;;
esac
if test "x$have_pcl" != xno; then
	save_CPPFLAGS="$CPPFLAGS"
	CPPFLAGS="$CPPFLAGS $PCL_CFLAGS"
	AC_CHECK_HEADER(pcl.h, have_pcl_h=yes,
		[ if test "x$have_pcl" != xauto; then AC_MSG_ERROR([pcl.h not found]); fi; have_pcl_h=no ])
	CPPFLAGS="$save_CPPFLAGS"

	if test "x$have_pcl_h" = xyes; then
		save_LIBS="$LIBS"
		LIBS="$LIBS $PCL_LIBS"
		AC_CHECK_LIB(pcl, PCLinit, PCL_LIBS="$PCL_LIBS -lpcl"
			AC_DEFINE(HAVE_LIBPCL, 1, [Define if you have the pcl library]) have_pcl=yes, 
		 	if test "x$have_pcl" = xyes; then
				save_LIBS="$LIBS"
				LIBS="$LIBS $PCL_LIBS"
				AC_CHECK_LIB(pcl, PCLexit, PCL_LIBS="$PCL_LIBS -lpcl -lperfctr"
					AC_DEFINE(HAVE_LIBPCL, 1, [Define if you have the pcl library]) have_pcl=yes,
					[ if test "x$have_pcl" != xauto; then AC_MSG_ERROR([libpcl not found]); fi; have_pcl=no ],
					"-lperfctr")
			fi
		)
		LIBS="$save_LIBS"
	else
		have_pcl=no
	fi

	if test "x$have_pcl" != xyes; then
		PCL_CFLAGS=""
		PCL_LIBS=""
	fi
fi
AC_SUBST(PCL_CFLAGS)
AC_SUBST(PCL_LIBS)

dnl provide different versions of the paths derived above
AM_MONETDB_XQ_VARS_2()
AC_DEFINE_UNQUOTED(MONETDB4_PREFIX,"$QXprefix",[architecture-independent files])
AC_DEFINE_UNQUOTED(MONETDB4_EXEC_PREFIX,"$QXexec_prefix",[architecture-dependent files])
QMONETDB4_BUILD="$MONETDB4_BUILD"
XMONETDB4_BUILD="`$translatepath "$MONETDB4_BUILD"`"
QXMONETDB4_BUILD="`echo "$XMONETDB4_BUILD" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QMONETDB4_BUILD)
AC_SUBST(XMONETDB4_BUILD)
AC_SUBST(QXMONETDB4_BUILD)
QMONETDB4_SOURCE="$MONETDB4_SOURCE"
XMONETDB4_SOURCE="`$translatepath "$MONETDB4_SOURCE"`"
QXMONETDB4_SOURCE="`echo "$XMONETDB4_SOURCE" | sed 's/\\\\/\\\\\\\\/g'`"
AC_SUBST(QMONETDB4_SOURCE)
AC_SUBST(XMONETDB4_SOURCE)
AC_SUBST(QXMONETDB4_SOURCE)
AC_DEFINE_UNQUOTED(MONETDB4_LOCALSTATEDIR,"$QXlocalstatedir",[modifiable single-machine data])
AC_DEFINE_UNQUOTED(MONETDB4_LIBDIR,"$QXlibdir",[object code libraries])
AC_DEFINE_UNQUOTED(MONETDB4_SYSCONFDIR,"$QXsysconfdir",[read-only single-machine data])
AC_DEFINE_UNQUOTED(MONETDB4_CONFFILE,"$QXsysconfdir${QDIRSEP}MonetDB.conf",[MonetDB4 config file])


dnl AC_CHECK_PROG(CP,cp,cp -f)
dnl AC_CHECK_PROG(MKDIR,mkdir,mkdir -p)
dnl AC_CHECK_PROG(TAGS,ctags,ctags)

dnl get monet compiler options (debug,warning,optimize,profiling,instrument)

dnl Checks for header files.
AC_HEADER_DIRENT()
AC_HEADER_SYS_WAIT()
dnl Niels said (when this was still revision 1.141 of MonetDB/configure.ag):
dnl OpenBSD-3.4 port: changed configure check for malloc.h to use full CFLAGS
CFLAGS_bak="$CFLAGS"
CFLAGS="$CFLAGS $X_CFLAGS"
AC_CHECK_HEADER(malloc.h, AC_DEFINE(HAVE_MALLOC_H,1,[Define to 1 if you have the
 <malloc.h> header file.]),,[ ])
CFLAGS="$CFLAGS_bak"
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h pwd.h dlfcn.h)
AC_CHECK_HEADERS(time.h sys/time.h utime.h sys/utime.h)
AC_CHECK_HEADERS(sys/file.h sys/param.h sys/mman.h)
AC_CHECK_HEADERS(rlimit.h sys/resource.h)
AC_CHECK_HEADERS(string.h strings.h signal.h sys/socket.h sys/un.h termios.h)
AC_CHECK_HEADERS(ieeefp.h winsock.h xmmintrin.h)
dnl We don't need to check for these two: they're only present on
dnl Windows.  We list them here for completeness, since the
dnl corresponding defines are used.
dnl AC_CHECK_HEADERS(io.h winsock.h)

if test -z "$ac_includes_default"	# compatibility with autoconf 2.13
then
	ac_includes_default='#include <stdio.h>
#if HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#if HAVE_SYS_STAT_H
# include <sys/stat.h>
#endif
#if STDC_HEADERS
# include <stdlib.h>
# include <stddef.h>
#else
# if HAVE_STDLIB_H
#  include <stdlib.h>
# endif
#endif
#if HAVE_STRING_H
# if !STDC_HEADERS && HAVE_MEMORY_H
#  include <memory.h>
# endif
# include <string.h>
#endif
#if HAVE_STRINGS_H
# include <strings.h>
#endif
#if HAVE_INTTYPES_H
# include <inttypes.h>
#else
# if HAVE_STDINT_H
#  include <stdint.h>
# endif
#endif
#if HAVE_UNISTD_H
# include <unistd.h>
#endif'
fi

dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_TYPE_PID_T()
AC_CHECK_TYPE([pid_t],,
	      [AC_DEFINE_UNQUOTED([pid_t], [int],
                                  [Define to 'int' if <sys/types.h> does not define.])],
	      [#include <stddef.h>
#include <sys/types.h>])

dnl AC_TYPE_OFF_T()
AC_CHECK_TYPE([off_t],,
	      [AC_DEFINE_UNQUOTED([off_t], [long],
                                  [Define to 'long' if <sys/types.h> does not define.])],
	      [#include <stddef.h>
#include <sys/types.h>])

AC_HEADER_STAT()
AC_HEADER_TIME()
AC_STRUCT_TM()
AC_STRUCT_TIMEZONE()

AC_TRY_COMPILE([$ac_includes_default
#include <malloc.h>], [struct mallinfo m;], AC_DEFINE(HAVE_STRUCT_MALLINFO, 1, [Define if you have struct mallinfo])
AC_TRY_COMPILE([$ac_includes_default
#include <malloc.h>], [struct mallinfo m = {0};
/* compilers that don't like <0 comparisons on unsigned data *
 * will fail here (but then not in src/gdk/gdk_utils.mx)     */
int x = (m.usmblks < 0);], AC_DEFINE(HAVE_SIGNED_MALLINFO, 1, [Define if your mallinfo struct has signed elements])
))
AC_CACHE_CHECK(for _sys_siglist, mn_cv_have__sys_siglist, [
AC_TRY_LINK([$ac_includes_default], [printf("%s\n", _sys_siglist[0]);], mn_cv_have__sys_siglist=yes, mn_cv_have__sys_siglist=no)])
if test $mn_cv_have__sys_siglist = yes; then
	AC_DEFINE(HAVE__SYS_SIGLIST, 1, [Define if you have _sys_siglist])
fi

AC_COMPILE_IFELSE(AC_LANG_SOURCE([int foo(int * restrict p) { return *p; }]),
AC_DEFINE(HAVE_RESTRICT, 1, [Define if the compiler supports the restrict keyword]),
AC_COMPILE_IFELSE(AC_LANG_SOURCE([int foo(int * __restrict__ p) { return *p; }]),
AC_DEFINE(HAVE___RESTRICT__, 1, [Define if the compiler supports the __restrict__ keyword])))

dnl Checks for library functions.
AC_FUNC_ALLOCA()
AC_FUNC_MMAP()
dnl  autoconf says, we cannot use AC_FUNC_SETPGRP when cross-compiling...
if test x$cross_compiling != xyes; then
	AC_FUNC_SETPGRP()
fi
AC_TYPE_SIGNAL()
AC_FUNC_UTIME_NULL()
AC_FUNC_VPRINTF()
AC_FUNC_MEMCMP()
AC_CHECK_FUNCS(strsignal)
AC_CHECK_FUNCS(mkdir rmdir getcwd getopt getrlimit vsnprintf snprintf kill pipe setsid)
AC_CHECK_FUNCS(strcspn strdup strstr strtod strtol strerror strcasecmp strncasecmp)
AC_CHECK_FUNCS(strtoll strtoull)
AC_CHECK_FUNCS(gethostname setenv putenv select)
AC_CHECK_FUNCS(fstat getpwuid getuid ftime sysconf fcntl shutdown)
AC_CHECK_FUNCS(mrand48 drand48)
AC_CHECK_FUNCS(nanosleep lockf)
AC_CHECK_FUNCS(ctime_r asctime_r localtime_r isinf fpclassify fpclass getlogin)
AC_CHECK_FUNCS(madvise posix_madvise posix_fadvise uname)
AC_CHECK_FUNCS(readdir rewinddir closedir)

asctime_r3=yes
AC_MSG_CHECKING(asctime_r3)
AC_TRY_LINK([$ac_includes_default
#include <time.h>], 
[char buf[26]; struct tm t; asctime_r(&t,buf,26);],
[AC_DEFINE(HAVE_ASCTIME_R3, 1, [Define if you have asctime_r(struct tm*,char *buf,size_t s)])], [asctime_r3=no])
AC_MSG_RESULT($asctime_r3)

ctime_r3=yes
AC_MSG_CHECKING(ctime_r3)
AC_TRY_LINK([$ac_includes_default
#include <time.h>], 
[char buf[26]; time_t t; ctime_r(&t,buf,26);],
[AC_DEFINE(HAVE_CTIME_R3, 1, [Define if you have ctime_r(time_t*,char *buf,size_t s)])], [ctime_r3=no])
AC_MSG_RESULT($ctime_r3)

AM_MONETDB_UTILS()
AM_MONETDB_MEL()

AM_CONDITIONAL(CROSS_COMPILING, test x$cross_compiling = xyes)

dnl allow us to keep track of how we are going to build this thing
compilercall="$CC $CFLAGS $X_CFLAGS"
AC_SUBST(compilercall)
linkercall="$LD $LDFLAGS"
AC_SUBST(linkercall)
builtby="${USER}@`hostname`"
AC_SUBST(builtby)
pcreversion="(not linked to any PCRE library)"
if test "x$have_pcre" = xyes; then
	pcreversion="compiled with `$PCRE_CONFIG --version 2>/dev/null`"
fi
AC_SUBST(pcreversion)
opensslversion="(not linked to any openssl library)"
if test "x$have_openssl" != xno; then
	opensslversion="compiled with `openssl version 2>/dev/null`"
fi
AC_SUBST(opensslversion)

dnl  CFLAGS for our code are stricter than what autoconf can cope with.
CFLAGS="$CFLAGS \$(X_CFLAGS)"
